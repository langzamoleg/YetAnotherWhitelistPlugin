plugins {
    id 'java'
    id 'eclipse'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
    id 'io.github.goooler.shadow' version '8.1.7'
    id 'xyz.jpenilla.run-velocity' version '2.3.1'
}

group = 'org.renwixx'
version = '1.2.1'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'
    
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'net.kyori:adventure-text-minimessage:4.17.0'
    implementation 'redis.clients:jedis:5.1.0'
}

shadowJar {
    archiveClassifier.set('')
    relocate 'com.moandjiezana.toml', 'org.renwixx.yawl.libs.toml'
    relocate 'net.kyori.adventure.text.minimessage', 'org.renwixx.yawl.libs.minimessage'
    relocate 'redis.clients', 'org.renwixx.yawl.libs.redis'
    relocate 'org.apache.commons.pool2', 'org.renwixx.yawl.libs.pool2'
    relocate 'org.json', 'org.renwixx.yawl.libs.json'
    relocate 'com.google.gson', 'org.renwixx.yawl.libs.gson'
}

build {
    dependsOn shadowJar
}

runVelocity {
    velocityVersion '3.4.0-SNAPSHOT'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')

def generateTemplates = tasks.register('generateTemplates', Copy) {
    inputs.property 'version', project.version
    from templateSource
    into templateDest
    expand 'version': project.version
}

sourceSets.main.java.srcDir generateTemplates.map { it.outputs }

idea.project.settings.taskTriggers.afterSync generateTemplates
eclipse.synchronizationTasks generateTemplates
